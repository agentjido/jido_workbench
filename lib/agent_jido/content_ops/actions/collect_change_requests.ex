defmodule AgentJido.ContentOps.Actions.CollectChangeRequests do
  @moduledoc """
  Aggregates work orders into content change request signals.

  In the full system, this collects `content.change_request` signals from
  creation agents. In the spike, it synthesizes change requests from work
  orders produced by SelectWork.
  """
  use Jido.Action,
    name: "contentops_collect_change_requests",
    description: "Aggregates work orders into content change requests",
    schema: [
      run_id: [type: :string, required: true, doc: "Current run identifier"],
      work_orders: [type: {:list, :map}, default: [], doc: "Work orders from SelectWork"],
      mode: [type: :any, required: true, doc: "Run cadence mode"],
      started_at: [type: :any, doc: "Run start timestamp"]
    ]

  @impl true
  def run(%{run_id: run_id, work_orders: work_orders, mode: mode} = params, _context) do
    change_requests = Enum.map(work_orders, &build_change_request(run_id, &1))

    {:ok,
     %{
       run_id: run_id,
       mode: mode,
       started_at: params[:started_at],
       change_requests: change_requests,
       change_request_count: length(change_requests)
     }}
  end

  defp build_change_request(run_id, work_order) do
    %{
      type: "content.change_request",
      run_id: run_id,
      authoring_agent: "ContentOps.OrchestratorAgent",
      changes: [
        %{
          op: :create,
          path: "priv/pages/docs/#{work_order.slug}.md",
          content: stub_content(work_order),
          rationale: "Spike: synthetic content for #{work_order.kind} work order #{work_order.id}"
        }
      ],
      validations_required: [:schema],
      related_plan_slug: work_order.slug
    }
  end

  defp stub_content(work_order) do
    """
    ---
    title: "#{work_order.slug}"
    kind: #{work_order.kind}
    status: draft
    ---

    # #{work_order.slug}

    Stub content generated by ContentOps spike.
    Work order: #{work_order.id}
    Priority: #{work_order.priority_score}
    """
  end
end
